#!/usr/bin/env -S just --justfile

# File synced from https://github.com/FasterSpeeding/Aurora-Fauxy
# Changes will not be persisted

set unstable := true
set shell := ["bash", "-eu", "-c"]
set script-interpreter := ["bash", "-eu"]

export JUST_TASKS_VERBOSE := env('JUST_TASKS_VERBOSE', '')

# Default command
default:
    just --list

# Internal function used for executing bash script tasks
[script]
execute-bash-script script-name *FLAGS:
    path="{{ source_directory() }}/bash_scripts/{{ script-name }}.bash"

    if [[ -n "$JUST_TASKS_VERBOSE" ]]
    then
        echo "Execution bash script at $path"
    fi

    bash -eu "$path" {{ FLAGS }}

# Install the default applications and libraries
[script]
install-defaults:
    just execute-bash-script install_defaults

# Sync the global user configuration symlinks
[script]
sync-config-symlinks:
    just execute-bash-script sync_config_symlinks

# Update the OS and installed dependencies.
[script]
update:
    just execute-bash-script update
